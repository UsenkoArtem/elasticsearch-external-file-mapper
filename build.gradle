buildscript {
    def esVersion = System.getProperty('esVersion', '5.6.8')

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${esVersion}"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
}

apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

esplugin {
    name = 'mapper-external-file'
    description 'External file field mapper for ElasticSearch'
    classname = 'company.evo.elasticsearch.plugin.mapper.ExternalFileMapperPlugin'
}

// TODO Remove all that stuff after upgrading Elasticsearch to 6.x
ext.projectSubstitutions = [:]
if (project.hasProperty('licenseFile')) {
    project.licenseFile = project.file('LICENSE.txt')
}
if (project.hasProperty('noticeFile')) {
    project.noticeFile = project.file('NOTICE.txt')
}

def version = project.file('project.version')
        .newDataInputStream()
        .newReader()
        .readLine()
        .toUpperCase()
if (hasProperty('release') && version.endsWith('-SNAPSHOT')) {
    version = version.substring(0, version.length() - 9)
}
project.version = "${version}-es${versions.elasticsearch}"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.apache.httpcomponents:httpcore:${versions.httpcore}"
    compile "org.apache.httpcomponents:httpclient:${versions.httpclient}"
    compile "commons-logging:commons-logging:${versions.commonslogging}"
    compile "net.sf.trove4j:trove4j:3.0.3"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

task testFunc(type: Test) {
    include '**/*TestCase.class'
}

testFunc.outputs.upToDateWhen { false }
