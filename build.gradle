buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:5.5.2"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
}

apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

esplugin {
    name = 'mapper-external-file'
    description 'External file field mapper for ElasticSearch'
    classname = 'company.evo.elasticsearch.plugin.mapper.ExternalFileMapperPlugin'
}

project.version = project.file('project.version')
        .newDataInputStream()
        .newReader()
        .readLine()
        .toUpperCase()

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.apache.httpcomponents:httpcore:${versions.httpcore}"
    compile "org.apache.httpcomponents:httpclient:${versions.httpclient}"
    compile "commons-logging:commons-logging:${versions.commonslogging}"
    compile "net.sf.trove4j:trove4j:3.0.3"
    compile project(':htable-java')
}

task testFunc(type: Test) {
    include '**/*TestCase.class'
}

testFunc.outputs.upToDateWhen { false }
